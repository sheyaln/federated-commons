resource "authentik_brand" "default" {
  domain  = local.gateway_domain
  default = true

  # Visual branding
  branding_title                   = "${local.org_name} Gateway"
  branding_logo                    = var.branding_logo
  branding_favicon                 = var.branding_favicon
  branding_default_flow_background = var.branding_default_flow_background

  branding_custom_css = file("${path.module}/assets/branding.css")

  lifecycle {
    ignore_changes = [branding_custom_css]
  }

  # Default flows - using username+passkey flow as default
  flow_authentication = module.flows.authentication_flow_login
  flow_invalidation   = module.flows.default_invalidation_flow_id
  flow_user_settings  = module.flows.default_user_settings_flow_id
  flow_recovery       = module.flows.password_reset_flow_uuid
  flow_unenrollment   = module.flows.default_unenrollment_flow_uuid

  # Global UI defaults - any user/group attribute can still override them  
  attributes = jsonencode({
    settings = {
      navbar = {
        userDisplay = "username"
      }
      theme = {
        base       = "automatic" # automatic/dark/light
      }

      enabledFeatures = {
        apiDrawer          = false
        applicationEdit    = false
        notificationDrawer = true
        search             = true
        settings           = true
      }

    }
  })
}
